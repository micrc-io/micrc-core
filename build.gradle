plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group = 'io.micrc.core'
version = '0.0.1'
sourceCompatibility = '11'

bootJar.enabled = false
jar.enabled = true

ext {
    set('camel.version', '3.18.1')
    set("hibernateTypesVersion", '2.18.0')
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "${colibri_nexus}"
        credentials {
            username "${colibri_nexus_user}"
            password "${colibri_nexus_pass}"
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.apache.camel.springboot:camel-spring-boot-bom:${property('camel.version')}"
    }
}

dependencies {
    // annotation - micrc-annotations: gradle task - publishToMavenLocal
    implementation "io.micrc.core:micrc-annotations:0.0.1"

    // for lombok
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    // persistence - db for dev
    implementation "com.h2database:h2"

    // integration camel for spring-boot base
    implementation "org.apache.camel.springboot:camel-spring-boot-starter"
    // bean integration
    implementation "org.apache.camel.springboot:camel-bean-starter"

    // json jackson support
    implementation "org.apache.camel.springboot:camel-jackson-starter"

    // dynamic route support
    implementation "org.apache.camel.springboot:camel-dynamic-router-starter"

    // json path support
    implementation "org.apache.camel.springboot:camel-jsonpath-starter"

    // jslt support
    implementation "org.apache.camel.springboot:camel-jslt-starter"

    // groovy support
    implementation "org.apache.camel.springboot:camel-groovy-starter"

    // json patch support
    implementation "org.apache.camel.springboot:camel-json-patch-starter"

    // http/websocket endpoints for consuming and producing, used by rest openapi and rest
    implementation "org.apache.camel.springboot:camel-undertow-starter"
    // openapi producer - derivatives producer adapter
    implementation "org.apache.camel.springboot:camel-rest-openapi-starter"
    // rest consumer - presentations and derivatives consumer adapter
    implementation "org.apache.camel.springboot:camel-rest-starter"

    // message endpoints for consuming and producing - message producer and consumer adapter
    implementation "org.apache.camel.springboot:camel-spring-rabbitmq-starter"
    // jpa repository adapter
    implementation "org.apache.camel.springboot:camel-jpa-starter"
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    // jpa-json-plugin
    implementation "com.vladmihalcea:hibernate-types-55:${hibernateTypesVersion}"

    // unit/integrate test
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

// 执行publishToMavenLocal后，其他工程可正常引入
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

nexusPublishing {
    repositories {
        create("colibri") {
            nexusUrl.set(uri("${colibri_nexus}"))
            snapshotRepositoryUrl.set(uri("${colibri_nexus}"))
            username.set("${colibri_nexus_user}")
            password.set("${colibri_nexus_pass}")
        }
    }
}
